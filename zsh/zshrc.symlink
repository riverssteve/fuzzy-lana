#!/usr/bin/env zsh

# uncomment this and the last line for zprof info
# zmodload zsh/zprof

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# {{{ zplug: Zsh Plugin Manager
source ~/.zplug/init.zsh

zplug "denysdovhan/spaceship-prompt"
zplug "mafredri/zsh-async"
zplug "rupa/z"
zplug "zsh-users/zsh-syntax-highlighting"

# Install plugins if there are plugins that have not been installed
if ! zplug check --verbose; then
    printf "Install? [y/N]: "
    if read -q; then
        echo; zplug install
    fi
fi

# Then, source plugins and add commands to $PATH
zplug load
# }}}

source $DOTFILES/zsh/pure.zsh

# use .config/secrets for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
[ -f ~/.config/secrets ] && . ~/.config/secrets

# load everything but the path and completion files
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/profile.zsh} ; do
    source "$file"
done

autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
    compinit
done
compinit -C

eval "$(dircolors $HOME/.dircolors)"
eval "$(direnv hook zsh)"

[ -f "$DOTFILES/zsh/zstyle.zsh" ] && {
    source "$DOTFILES/zsh/zstyle.zsh"
}

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh} ; do
    source "$file"
done

unset config_files

# Load custom work stuff
[ -f ~/.config/work.zsh ] && . ~/.config/work.zsh

# pyenv needs to be manually defined at the end since it manipulates path
if command -v pyenv 1>/dev/null 2>&1; then
    eval "$(pyenv init - zsh --no-rehash)"
fi

# zprof

# vim:foldmethod=marker:foldlevel=0
