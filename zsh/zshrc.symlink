# uncomment this and the last line for zprof info
# zmodload zsh/zprof

# # all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# {{{ Zinit
if [[ ! -f $HOME/.zinit/bin/zinit.zsh ]]; then
    print -P "%F{33}▓▒░ %F{220}Installing %F{33}DHARMA%F{220} Initiative Plugin Manager (%F{33}zdharma/zinit%F{220})…%f"
    command mkdir -p "$HOME/.zinit" && command chmod g-rwX "$HOME/.zinit"
    command git clone https://github.com/zdharma/zinit "$HOME/.zinit/bin" && \
        print -P "%F{33}▓▒░ %F{34}Installation successful.%f%b" || \
        print -P "%F{160}▓▒░ The clone has failed.%f%b"
fi

source "$HOME/.zinit/bin/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zinit light-mode for \
    zinit-zsh/z-a-rust \
    zinit-zsh/z-a-as-monitor \
    zinit-zsh/z-a-patch-dl \
    zinit-zsh/z-a-bin-gem-node

zinit wait lucid for \
    rupa/z \
    zsh-users/zsh-syntax-highlighting \
    zsh-users/zsh-autosuggestions \
    zdharma/fast-syntax-highlighting \
    zdharma/history-search-multi-word

# https://github.com/zdharma/zinit#calling-compinit-with-turbo-mode
zinit wait lucid atload"zicompinit; zicdreplay" blockf for \
    zsh-users/zsh-completions
# }}}

# load everything but the path, completion and profile files
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/profile.zsh} ; do
    source "$file"
done

# Run compinit every 24 hours
autoload -Uz compinit
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
    compinit
    touch ~/.zcompdump
else
    compinit -C
fi

# Load custom work stuff
[ -f ~/.config/work.zsh ] && . ~/.config/work.zsh

# use .config/secrets for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
[ -f ~/.config/secrets ] && . ~/.config/secrets

eval "$(starship init zsh)"

# pyenv needs to be manually defined at the end since it manipulates path
if command -v pyenv 1>/dev/null 2>&1; then
    eval "$(pyenv init - zsh --no-rehash)"
fi

#zprof

# vim:foldmethod=marker:foldlevel=0

