#!/usr/bin/env zsh
# uncomment this and the last line for zprof info
# zmodload zsh/zprof

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.dotfiles"

# project folder that we can `c [tab]` to
export PROJECTS="$HOME/code"

# Set editor defaults
export EDITOR='vim'
export VEDITOR='code'

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load zsh, antibody and plugins
source ~/.zsh_plugins.sh
source $DOTFILES/zsh/pure.zsh

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh} ; do
    echo "Sourcing files ${file}"
    source $file
done

autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh} ; do
    echo "Sourcing completion file ${file}"
    source $file
done

unset config_files